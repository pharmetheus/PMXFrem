;; 1. Based on: 3
;; 2. Description:
;;    GOF for frem
;; 3. Label:
;;    SimVal base model
;------------------------------------------------------------------------------
$PROBLEM    run 1
$INPUT      NO ID STUDYID TAD TIME DAY AMT RATE ODV DV EVID BLQ DOSE
            FOOD FORM TYPE WT HT LBWT BSA SEX RACE AGE AST ALT BILI
            CRCL BMI NCI GENO ETHNIC SMOK RACEL GENOL NCIL 
            FRELFREMCOV CLFREMCOV VFREMCOV MATFREMCOV
$DATA      vpcData2.csv IGNORE=@

$SUBROUTINE ADVAN2 TRANS2
$PK
;;; MATFOOD-DEFINITION START
IF(FOOD.EQ.1) MATFOOD = 1  ; Most common
IF(FOOD.EQ.0) MATFOOD = ( 1 + THETA(6))
;;; MATFOOD-DEFINITION END

;;; MAT-RELATION START
MATCOVTIME = MATFOOD
;;; MAT-RELATION END


;;; FRELFOOD-DEFINITION START
IF(FOOD.EQ.1) FRELFOOD = 1  ; Most common
IF(FOOD.EQ.0) FRELFOOD = ( 1 + THETA(7))
;;; FRELFOOD-DEFINITION END

;;; FREL-RELATION START
FRELCOVTIME = FRELFOOD
;;; FREL-RELATION END

TVFREL  = THETA(1)
TVCL    = THETA(2)
TVV     = THETA(3)
TVMAT   = THETA(4)
TVD1    = THETA(5)

;MU_1  = LOG(TVRUV)
MU_2  = TVD1
MU_3  = LOG(TVFREL)
MU_4  = LOG(TVCL)
MU_5  = LOG(TVV)
MU_6  = LOG(TVMAT)

D1FR  = MU_2                   + ETA(2)
FREL  = FRELCOVTIME * EXP(MU_3 + ETA(3) + FRELFREMCOV)
CL    = EXP(MU_4               + ETA(4) + CLFREMCOV) 
V     = EXP(MU_5               + ETA(5) + VFREMCOV)  
MAT   = MATCOVTIME * EXP(MU_6  + ETA(6) + MATFREMCOV)
D1    = MAT*(1-D1FR)

F1    = FREL
KA    = 1 / (MAT-D1)
S2    = V

$ERROR
CP    = A(2)*1000 / V
IPRED = LOG(CP + 0.00001)
Y     = IPRED + EPS(1) * EXP(ETA(1))

REPI = IREP

$THETA  1 FIX ; 1. TVFREL
$THETA  (0,7.61332) ; 2. TVCL
$THETA  (0,149.936) ; 3. TVV
$THETA  (0,1.9975) ; 4. TVMAT
$THETA  (0,0.681315) ; 5. D1
$THETA  (-1.00,-0.086402,3.00) ; 6. FRELFOOD1
$THETA  (-1.00,0.103431,3.00) ; 7. MATFOOD1
$OMEGA  0.114627  ; 1. IIV on RUV
$OMEGA  0.0001  FIX  ;         D1
$OMEGA BLOCK(4) 
0.25671
-0.03111 0.12963
-0.07285 -0.1295 0.18477
0.0124 -0.01055 0.02779 0.13789
$SIGMA  0.0320067  ;     1. RUV
$ESTIMATION METHOD=1 INTER ETASTYPE=1 MAXEVAL=0 PRINT=1

$TABLE      NO ID STUDYID TAD TIME DAY AMT RATE ODV DV EVID BLQ DOSE
            FOOD FORM TYPE WT HT LBWT BSA SEX RACE AGE AST ALT BILI
            CRCL BMI NCI GENO ETHNIC SMOK RACEL GENOL NCIL CPRED
            CIPREDI CWRES CIWRES ETAS(1:LAST) NOPRINT ONEHEADER
            FILE=xptabmax04
