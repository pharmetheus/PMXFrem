;; 1. Based on: 7
;; 2. Description:
;;    IMPMAP and FULL BLOCK
;; 3. Label:
;;    Base model
;------------------------------------------------------------------------------
$PROBLEM    run 1
$INPUT      NO ID STUDYID TAD TIME DAY AMT RATE ODV DV EVID BLQ DOSE
            FOOD FORM TYPE WT HT AGE SEX RACE ETHNIC GENO SMOK AST ALT
            BILI BMI CRCL NCI NCIL RACEL RACEL1 RACEL2 RACEL3 GENO1
            GENO2 GENO3 GENO4
$DATA      ../../ProducedData/Dataset/DAT-1-MI-PMX-2.csv IGNORE=@
            IGNORE(TYPE.EQN.1) IGNORE(BLQ.EQN.1) IGNORE(TAD.GT.100)
$SUBROUTINE ADVAN2 TRANS2
$PK
;;; MATFOOD-DEFINITION START
IF(FOOD.EQ.1.0000E+00) MATFOOD = 1  ; Most common
IF(FOOD.EQ.0.0000E+00) MATFOOD = ( 1 + THETA(9))
;;; MATFOOD-DEFINITION END

;;; MAT-RELATION START
MATCOVTIME = MATFOOD
;;; MAT-RELATION END


;;; FRELFOOD-DEFINITION START
IF(FOOD.EQ.1.0000E+00) FRELFOOD = 1  ; Most common
IF(FOOD.EQ.0.0000E+00) FRELFOOD = ( 1 + THETA(8))
;;; FRELFOOD-DEFINITION END

;;; FREL-RELATION START
FRELCOVTIME = FRELFOOD
;;; FREL-RELATION END

CLWT    = (WT/75)**THETA(1)
CLCOV   = CLWT

VWT     = (WT/75)**THETA(2)
VCOV    = VWT
 
TVFREL  = THETA(3)
TVCL    = THETA(4) * CLCOV
TVV     = THETA(5) * VCOV
TVMAT   = THETA(6)
TVD1    = THETA(7)

;MU_1  = LOG(TVRUV)
MU_2  = TVD1
MU_3  = LOG(TVFREL)
MU_4  = LOG(TVCL)
MU_5  = LOG(TVV)
MU_6  = LOG(TVMAT)

D1FR  = MU_2                   + ETA(2)
FREL  = FRELCOVTIME * EXP(MU_3 + ETA(3))
CL    = EXP(MU_4               + ETA(4))
V     = EXP(MU_5               + ETA(5))
MAT   = MATCOVTIME * EXP(MU_6  + ETA(6))
D1    = MAT*(1-D1FR)

F1    = FREL
KA    = 1 / (MAT-D1)
S2    = V

$ERROR
CP    = A(2)*1000 / V
IPRED = LOG(CP + 0.00001)
Y     = IPRED + EPS(1) * EXP(ETA(1))

$THETA  0.75 FIX ; 1. WT coef on TVCL
$THETA  1 FIX ; 2. WT coef on TVV
$THETA  1 FIX ; 3. TVFREL
$THETA  (0,7.03728) ; 4. TVCL
$THETA  (0,133.898) ; 5. TVV
$THETA  (0,2.08608) ; 6. TVMAT
$THETA  (0,0.681472) ; 7. D1
$THETA  (-1.00,0.107997,3.00) ; FRELFOOD1
$THETA  (-1.00,-0.081275,3.00) ; MATFOOD1
$OMEGA  0.0424951  ; 1. IIV on RUV
$OMEGA  0.0001  FIX  ;         D1
$OMEGA  BLOCK(4)
 0.243643  ; 2. IIV on FREL
 0.102768 0.22  ; 3. IIV on CL
 0.122407 0.106783 0.38  ; 4. IIV on V
 0.0025675 0.0160454 0.0551753 0.149201  ; 5. IIV on MAT
$SIGMA  0.0324511  ;     1. RUV
$ESTIMATION METHOD=IMPMAP AUTO=1 RANMETHOD=3P INTER NOABORT PRINT=1
            CITER=15 NOCOV=1 ISAMPEND=100 NITER=50 ISAMPLE=300
            CTYPE=1 CALPHA=0.001
$ESTIMATION METHOD=IMPMAP INTER EONLY=1 NITER=5 ISAMPLE=1000 NOCOV=0
$COVARIANCE PRINT=E MATRIX=S UNCONDITIONAL
$TABLE      NO ID STUDYID TAD TIME DAY AMT RATE ODV DV EVID BLQ DOSE
            FOOD FORM TYPE WT HT AGE SEX RACE ETHNIC GENO SMOK AST ALT
            BILI BMI CRCL NCI NCIL RACEL RACEL1 RACEL2 RACEL3 GENO1
            GENO2 GENO3 GENO4 CPRED CIPREDI CWRES CIWRES ETAS(1:LAST)
            NOPRINT ONEHEADER FILE=xptab8

